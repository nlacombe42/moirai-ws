buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.2.RELEASE'
		classpath 'se.transmode.gradle:gradle-docker:1.2'
	}
}

apply plugin: 'org.springframework.boot'
apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'io.spring.dependency-management'

defaultTasks 'tasks'

group = 'net.nlacombe'
version = '0.0.2'

sourceCompatibility = 11
targetCompatibility = 11

project.ext.gcloudBuildDir = file('build/appengine/')
project.ext.gcloudProjectId = 'nlacombe-net-prod'

def String gcloudDockerImageUrl

dependencies {
	implementation 'net.nlacombe:common-lib:10.4.0'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'javax.inject:javax.inject:1'
	implementation 'com.google.api-client:google-api-client:1.23.0'
	implementation 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
	implementation 'com.google.apis:google-api-services-calendar:v3-rev305-1.23.0'
	implementation 'org.mnode.ical4j:ical4j:3.0.6'
	implementation 'org.ehcache:jcache:1.0.1'
	implementation 'org.apache.commons:commons-collections4:4.2'
	implementation 'commons-beanutils:commons-beanutils:1.9.3'

	implementation 'org.slf4j:slf4j-api:1.7.25'
	runtime 'ch.qos.logback:logback-classic:1.2.3'
}

repositories {
	mavenCentral()
	mavenLocal()
}

wrapper {
	gradleVersion = '6.5'
}

task buildLocalDockerImage(type: Docker, dependsOn: assemble) {
	tag = "${project.name}"
	dockerfile = file('src/main/docker/Dockerfile')
	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into stageDir
		}
	}
}

task buildGcloudDockerImage(type: Exec, dependsOn: build) {
	def buildStartTime = new Date()
	gcloudDockerImageUrl = getGcloudDockerImageUrl(buildStartTime)

	workingDir project.gcloudBuildDir
	commandLine 'gcloud', 'builds', 'submit', '--tag', gcloudDockerImageUrl

	doFirst {
		copy {
			from jar
			from file('src/main/docker/')
			into project.gcloudBuildDir
		}
	}
}

task deployToProdGcloud(type: Exec, dependsOn: buildGcloudDockerImage) {
	workingDir project.gcloudBuildDir
	commandLine 'gcloud', 'beta', 'run', 'deploy', "${project.name}",
			'--region', 'us-central1',
			'--memory', '500M',
			'--image', gcloudDockerImageUrl
}

def getGcloudDockerImageUrl(date) {
	def version = project.version + '-' + getDateTimestamp(date)

	return "gcr.io/${project.gcloudProjectId}/${project.name}:${version}"
}

def static getDateTimestamp(date) {
	return date.format('yyyyMMddHHmmss')
}
